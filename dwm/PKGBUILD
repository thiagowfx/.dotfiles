# $Id$
# Maintainer: Thiago Perrotta <perrotta dot thiago at poli dot ufrj dot br>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=dwm
pkgver=6.1
pkgrel=3
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'dmenu' 'lxterminal' 'i3-wm' 'ttf-pt-fonts')
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
	config.h
	dwm.desktop
	$pkgname-uselessgap-$pkgver.diff)
md5sums=('f0b6b1093b7207f89c2a90b848c008ec'
         'd62e2b3636cfaaeb71f4b6fb2528ac49'
         '939f403a71b6e85261d09fc3412269ee'
         '4f0b57c6b9331b0a90c97d9c14944609')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  cp "$srcdir"/config.h config.h

  patch -Np1 < "$srcdir/$pkgname-uselessgap-$pkgver.diff"
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README "$pkgdir"/usr/share/doc/$pkgname/README
  install -m644 -D "$srcdir"/dwm.desktop "$pkgdir"/usr/share/xsessions/dwm.desktop
}
