### vim:ft=zsh:foldmethod=marker
## search.cpan.org backend for lookup
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>

LOOKUP_guard || return 1
[[ -n ${lookup_describe} ]] &&
    printf '%s' 'search CPAN via search.cpan.org' && return 0

LOOKUP_guard -fd LOOKUP_help_${backend} ||
function LOOKUP_help_${backend}() {
    LOOKUP_guard || return 1
    printf 'usage: %s <query>\n' ${backend}
    printf '\n Searches CPAN via search.cpan.org.\n'
    printf '\nExamples:\n'
    printf ' %% lookup %s Audio::Scan\n' ${backend}
}
LOOKUP_help && return 0

if [[ -n ${lookup_complete} ]] ; then
    _message 'search.cpan.org query'
    return 0
fi

local -x QUERY
QUERY="$*"
LOOKUP_query_handler || return 1
if [[ -z ${QUERY} ]] ; then
    LOOKUP_help -f
    return 1
fi

LOOKUP_encode -q
LOOKUP_browser "http://search.cpan.org/search?query=${QUERY}"
return $?
