### vim:ft=zsh:foldmethod=marker
## filewatcher.com search backend for lookup
## Copyright: 2009, Frank Terbeck <ft@bewatermyfriend.org>

LOOKUP_guard || return 1
[[ -n ${lookup_describe} ]] &&
    printf '%s' 'search filewatcher.com' &&
    return 0

local -a comp_args

LOOKUP_guard -fd LOOKUP_help_${backend} ||
function LOOKUP_help_${backend}() {
    LOOKUP_guard || return 1
    printf 'usage: %s <query>\n' ${backend}
    printf '\n Use filewatcher.com to search FTP server contents.\n'
    printf '\nExamples:\n'
    printf ' %% lookup %s '\''zsh*.tar.gz'\''\n' ${backend}
    printf ' %% lookup %s '\''*.jpg'\''\n\n' ${backend}
}
LOOKUP_help && return 0

if [[ -n ${lookup_complete} ]] ; then
    _message 'filewatcher search string'
    return 0
fi

local -x QUERY

QUERY="$*"
LOOKUP_query_handler || return 1
if [[ -z ${QUERY} ]] ; then
    LOOKUP_help -f
    return 1
fi

LOOKUP_encode -s -q
LOOKUP_browser "http://www.filewatcher.com/_/?q=${QUERY}"
return $?
